#This script is a sub of termpointtognb.mos and cannot be used on it's own!

#*********Initial variables**********************
$linecontent = readfile(pathtotermpointtognb_mos)
$path = $linecontent[1]
$xtwonotcreatedongnbside = 0
#*********Initial variables**********************

l+ $path/log/xtworesultgnbsub.log
lt all

func getdata
	$linecontent = readfile($path/tmp/sequence.txt)
	$sequence = $linecontent[1]
	$linecontent = readfile($path/tmp/enbdata[$sequence].txt)
	$enodebid = $linecontent[1]
endfunc

func xtwocheck
	for $mo in extenbf
		$extenbfldn = ldn($mo)
	done
	lk $extenbfldn,TermPointToENodeB
	if $nr_of_mos = 0
		$xtwonotcreatedongnbside = 1
	else
		for $mo in lk_group
			$xtwoldn = ldn($mo)
		done
		get $xtwoldn operationalstate > $xtwoopstgnbside
	fi
endfunc


func extenbfcheck
		ma extenbf ExternalENodeBFunction eNodeBId ^$enodebid$
		if $nr_of_mos > 0
			xtwocheck
		else
			$xtwonotcreatedongnbside = 1
		fi
endfunc

func writedata
	# $linecontent[1] = $xtwonotcreatedongnbside
	# $linecontent[2] = $xtwoopstgnbside
	# $linecontent[3] = $xtwoldn
	
	!echo '$xtwonotcreatedongnbside' >>	$path/tmp/gnbresult[$sequence].txt
	!echo '$xtwoopstgnbside' >>			$path/tmp/gnbresult[$sequence].txt
	!echo '$xtwoldn' >>					$path/tmp/gnbresult[$sequence].txt
endfunc

getdata
extenbfcheck
writedata